import Std.Convert.ResultArrayAsInt;
import Std.Math.BitSizeI;


operation GenerateRandomBit(): Result{
    use q = Qubit();
    H(q);
    let result = M(q);
    Reset(q);

    return result;
}


operation GenerateRandomNumberInRange(max: Int): Int{
    mutable bits = [];
    // Determine how many bits are required to store `max`
    let nBits = BitSizeI(max);

    // Add the bits generated by `GenerateRandomBit()` 
    for idxBit in 1..nBits{
        set bits += [GenerateRandomBit()];
    }
    // Convert the bits to integer
    let sample = ResultArrayAsInt(bits);

    // Check wether the integer exceeds `max` or not
    return sample > max? GenerateRandomNumberInRange(max) | sample;
}

operation Main(): Int{
    let max = 100;
    Message($"Sampling a random number between 0 and {max}: ");
    return GenerateRandomNumberInRange(max);
}